/// # The regex Plugin {#regex}
///
/// @see thorin::regex
///
/// [TOC]
///
/// A normalizing regex plugin
///
/// ## Dependencies
///
.import core;
.import mem;
.import compile;
.plugin direct;
///
/// ## Types
///
/// ### RE
///
/// A regular expression matcher.
.let RE = Π[n: .Nat][%mem.M, %mem.Ptr0 «n; %core.I8», .Idx n] -> [%mem.M, .Bool, .Idx n];
.let CPS_RE = Π[n: .Nat] -> .Cn [[%mem.M, %mem.Ptr0 «n; %core.I8», .Idx n], .Cn[%mem.M, .Bool, .Idx n]];

/// ## Meta
///
/// ### %regex.conj
/// 
/// A sequence of RE's, e.g. `\d\d\d` matching 3 digits:
/// `%regex.conj (%regex.cls.d, %regex.cls.d, %regex.cls.d)
///
.ax %regex.conj: Π[n: .Nat][<<n; RE>>] -> RE, normalize_conj, 2;

///
/// ### %regex.disj
///
/// Match any of the sub expressions: `[0123456789]`
///
.ax %regex.disj: Π[n: .Nat][<<n; RE>>] -> RE, normalize_disj, 2;

///
/// ### %regex.group
///
/// A capture group
///
.ax %regex.group: Π[n: .Nat][<<n; RE>>] -> RE, normalize_group, 2;

///
/// ## Values
///
/// ### %regex.lit
///
/// Wraps a literal.
///
.ax %regex.lit: Π n:.Nat -> RE, normalize_lit, 1;

///
/// ### %regex.cls.*
/// 
/// | Subtag | Matches |
/// | ------ | ------- |
/// | `d`    | digits `[0-9]`|
/// | `D`    | No digits |
/// | `w`    | word characters `[a-zA-Z_0-9]` |
/// | `W`    | No word characters |
/// | `s`    | whitespace `[ \t\r\n]` |
/// | `S`    | No whitespace |
/// | `any`  | Any character |
.ax %regex.cls(d,D,w,W,s,S,any): RE, normalize_cls, 0;

/// ## Quantifiers
///
/// ### %regex.quant.*
///
.ax %regex.quant(zeroOrOne,zeroOrMore,oneOrMore): Π pattern: RE -> RE, normalize_quant, 1;

///
/// ## Implementation
///
.con %regex.match_d(n: .Nat)((mem: %mem.M, string: %mem.Ptr0 «n; %core.I8», pos: .Idx n), cont: .Cn[%mem.M, .Bool, .Idx n]) = {
    .let ptr = %mem.lea (n, ‹n; %core.I8›, 0) (string, pos);
    .let (`mem, char) = %mem.load (mem, ptr);
    .let is_digit = %core.bit2.and_ 0 (%core.icmp.uge (char, '0'),  %core.icmp.ule (char, '9'));
    
    .let new_pos = (pos, %core.conv.u n (%core.wrap.add 0 (%core.conv.u %core.i32 pos, 1:%core.I32)))#is_digit;
    cont (mem, is_digit, new_pos)
};
.con %regex.match_D(n: .Nat)((mem: %mem.M, string: %mem.Ptr0 «n; %core.I8», pos: .Idx n), cont: .Cn[%mem.M, .Bool, .Idx n]) = {
    .con ret_inverted (mem: %mem.M, is_digit: .Bool, new_pos: .Idx n) = {
        cont (mem, %core.bit1.neg 0 is_digit, new_pos)
    };
    %regex.match_d n ((mem, string, pos), ret_inverted)
};
.con %regex.match_w(n: .Nat)((mem: %mem.M, string: %mem.Ptr0 «n; %core.I8», pos: .Idx n), cont: .Cn[%mem.M, .Bool, .Idx n]) = {
    .let ptr = %mem.lea (n, ‹n; %core.I8›, 0) (string, pos);
    .let (`mem, char) = %mem.load (mem, ptr);
    .let is_upper_lit = %core.bit2.and_ 0 (%core.icmp.uge (char, 'A'),  %core.icmp.ule (char, 'Z'));
    .let is_lower_lit = %core.bit2.and_ 0 (%core.icmp.uge (char, 'a'),  %core.icmp.ule (char, 'z'));
    .let is_underscore = %core.icmp.e (char, '_');
    .let is_digit = %core.bit2.and_ 0 (%core.icmp.uge (char, '0'),  %core.icmp.ule (char, '9'));
    .let is_w = %core.bit2.or_ 0 (%core.bit2.or_ 0 (is_upper_lit, is_lower_lit), %core.bit2.or_ 0 (is_underscore, is_digit));
    
    .let new_pos = (pos, %core.conv.u n (%core.wrap.add 0 (%core.conv.u %core.i32 pos, 1:%core.I32)))#is_w;
    cont (mem, is_w, new_pos)
};
.con %regex.match_W(n: .Nat)((mem: %mem.M, string: %mem.Ptr0 «n; %core.I8», pos: .Idx n), cont: .Cn[%mem.M, .Bool, .Idx n]) = {
    .con ret_inverted (mem: %mem.M, is_w: .Bool, new_pos: .Idx n) = {
        cont (mem, %core.bit1.neg 0 is_w, new_pos)
    };
    %regex.match_w n ((mem, string, pos), ret_inverted)
};
.con %regex.match_s(n: .Nat)((mem: %mem.M, string: %mem.Ptr0 «n; %core.I8», pos: .Idx n), cont: .Cn[%mem.M, .Bool, .Idx n]) = {
    .let ptr = %mem.lea (n, ‹n; %core.I8›, 0) (string, pos);
    .let (`mem, char) = %mem.load (mem, ptr);
    .let is_space = %core.icmp.e (char, ' ');
    .let is_tab = %core.icmp.e (char, '\t');
    .let is_nf = %core.icmp.e (char, '\n');
    .let is_cr = %core.icmp.e (char, '\r');
    .let is_s = %core.bit2.or_ 0 (%core.bit2.or_ 0 (is_space, is_tab), %core.bit2.or_ 0 (is_nf, is_cr));
    
    .let new_pos = (pos, %core.conv.u n (%core.wrap.add 0 (%core.conv.u %core.i32 pos, 1:%core.I32)))#is_s;
    cont (mem, is_s, new_pos)
};
.con %regex.match_S(n: .Nat)((mem: %mem.M, string: %mem.Ptr0 «n; %core.I8», pos: .Idx n), cont: .Cn[%mem.M, .Bool, .Idx n]) = {
    .con ret_inverted (mem: %mem.M, is_s: .Bool, new_pos: .Idx n) = {
        cont (mem, %core.bit1.neg 0 is_s, new_pos)
    };
    %regex.match_s n ((mem, string, pos), ret_inverted)
};
.con %regex.match_any(n: .Nat)((mem: %mem.M, string: %mem.Ptr0 «n; %core.I8», pos: .Idx n), cont: .Cn[%mem.M, .Bool, .Idx n]) = {
    .let ptr = %mem.lea (n, ‹n; %core.I8›, 0) (string, pos);
    .let (`mem, char) = %mem.load (mem, ptr);
    .let isnt_0 = %core.icmp.ne (char, '\0');

    .let new_pos = (pos, %core.conv.u n (%core.wrap.add 0 (%core.conv.u %core.i32 pos, 1:%core.I32)))#isnt_0;
    cont (mem, isnt_0, new_pos)
};
.con %regex.match_conj (num_subexpr: .Nat)(subexprs: <<num_subexpr; CPS_RE>>)(n: .Nat)((mem: %mem.M, string: %mem.Ptr0 «n; %core.I8», pos: .Idx n), cont: .Cn[%mem.M, .Bool, .Idx n]) = {
    .let num_subexpr_int = %core.bitcast %core.I64 num_subexpr;
    .let n_int = %core.bitcast %core.I64 n;
    .con exit_true ((mem: %mem.M, idx: .Idx num_subexpr, pos: .Idx n)) = {
        cont (mem, 1:.Bool, pos)
    };

    .con exit_false ((mem: %mem.M, idx: .Idx num_subexpr, pos: .Idx n)) = {
        cont (mem, 0:.Bool, pos)
    };

    .con iter ((mem: %mem.M, idx: .Idx num_subexpr, pos: .Idx n)) = {
        .let idxprom = %core.conv.u n idx;
        //.let offset_pos = %core.wrap.add 0 (pos, idxprom);

        .con subexpr_ret(mem:%mem.M, matched:.Bool, new_pos:.Idx n) = {
            .con on_matched (mem: %mem.M, idx: .Idx num_subexpr, pos: .Idx n) = {
                .let idx64 = %core.conv.u %core.i64 idx;
                .let new_idx64 = %core.wrap.add 0 (idx64, 1:%core.I64);
                .let done = %core.icmp.e (new_idx64, num_subexpr_int);
                .let new_idx = %core.conv.u num_subexpr (new_idx64);
                (iter, exit_true)#done ((mem, new_idx, new_pos))
            };
            (exit_false, on_matched)#matched ((mem, idx, new_pos))
        };

        subexprs#idx n ((mem, string, pos), subexpr_ret)
    };
    
    // todo %core.ncmp.le (num_subexpr, n)
    // %direct.cps2ds ([%mem.M, .Idx num_subexpr, .Idx n], [%mem.M, .Bool, .Idx n]) iter (mem, 0:(.Idx num_subexpr), pos)
    iter ((mem, 0:(.Idx num_subexpr), pos))
};

/// ## Passes and Phases
/// 
/// ### Passes
/// 
.ax %regex.lower_regex: %compile.Pass;

