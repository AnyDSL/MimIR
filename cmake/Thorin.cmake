# clear globals
SET(THORIN_DIALECT_LIST    "" CACHE INTERNAL "THORIN_DIALECT_LIST") 
SET(THORIN_DIALECT_H_LIST  "" CACHE INTERNAL "THORIN_DIALECT_H_LIST")
SET(THORIN_DIALECT_MD_LIST "" CACHE INTERNAL "THORIN_DIALECT_MD_LIST")
SET(THORIN_DIALECT_LAYOUT  "" CACHE INTERNAL "THORIN_DIALECT_LAYOUT")

function(add_thorin_dialect DIALECT)
    set(THORIN_FILE ${CMAKE_CURRENT_SOURCE_DIR}/${DIALECT}/${DIALECT}.thorin)
    set(DIALECT_H  ${CMAKE_CURRENT_BINARY_DIR}/${DIALECT}.h)
    set(DIALECT_MD ${CMAKE_CURRENT_BINARY_DIR}/${DIALECT}.md)

    list(APPEND THORIN_DIALECT_LIST "${DIALECT}")
    list(APPEND THORIN_DIALECT_H_LIST  "${DIALECT_H}")
    list(APPEND THORIN_DIALECT_MD_LIST "${DIALECT_MD}")
    string(APPEND THORIN_DIALECT_LAYOUT "<tab type=\"user\" url=\"@ref ${DIALECT}\" title=\"${DIALECT}\"/>")

    # populate to globals
    SET(THORIN_DIALECT_LIST     "${THORIN_DIALECT_LIST}"    CACHE INTERNAL "THORIN_DIALECT_LIST")
    SET(THORIN_DIALECT_H_LIST   "${THORIN_DIALECT_H_LIST}"  CACHE INTERNAL "THORIN_DIALECT_H_LIST")
    SET(THORIN_DIALECT_MD_LIST  "${THORIN_DIALECT_MD_LIST}" CACHE INTERNAL "THORIN_DIALECT_MD_LIST")
    SET(THORIN_DIALECT_LAYOUT   "${THORIN_DIALECT_LAYOUT}"  CACHE INTERNAL "THORIN_DIALECT_LAYOUT")

    add_custom_command(
        OUTPUT ${DIALECT_MD} ${DIALECT_H}
        COMMAND thorin -e md -e h ${THORIN_FILE}
        MAIN_DEPENDENCY ${THORIN_FILE}
        DEPENDS thorin
        COMMENT "Bootstrapping Thorin dialect '${DIALECT}' from '${THORIN_FILE}'"
    )
    add_custom_target(${DIALECT} ALL DEPENDS ${DIALECT_MD} ${DIALECT_H})
endfunction()
