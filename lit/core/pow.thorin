// RUN: rm -f %t.ll
// RUN: %thorin %s --output-ll %t.ll -o - | FileCheck %s
//
// if b<=0:
//     1
// else
//     a*pow(a,b-1)
//
// pow(a,b,ret):
//     ((pow_else,pow_then)#cmp) ()
// then():
//    ret 1
// else():
//     pow(a,b-1,cont)
// cont(v):
//    ret (a*v)

.plugin core;

.fun pow(a b: %core.I32): %core.I32 =
    .con pow_cont(v: %core.I32) =
        return (%core.wrap.mul 0 (a, v));

    .con pow_then() =
        return 1:%core.I32;

    .con pow_else() =
        .let b_1 = %core.wrap.sub 0 (b, 1:%core.I32);
        pow ((a, b_1), pow_cont);

    .let cmp = %core.icmp.e (b, 0:%core.I32);
    (pow_else, pow_then)#cmp ();

.fun .extern main(mem: %mem.M, argc: %core.I32, argv: %mem.Ptr0 (%mem.Ptr0 %core.I8)): [%mem.M, %core.I32] =
    .con ret_cont(res: %core.I32) = return (mem, res);

    .let c = (42:%core.I32, 2:%core.I32);
    pow (c,ret_cont);
// CHECK-DAG: .con pow_{{[0-9_]+}} _{{[0-9_]+}}::[b_{{[0-9_]+}}: .Idx 4294967296, return_{{[0-9_]+}}: .Cn .Idx 4294967296]{{(@.*)?}}= {
// CHECK-DAG:     .con return_{{[0-9_]+}} _{{[0-9_]+}}: .Idx 4294967296{{(@.*)?}}= {
// CHECK-DAG:         return_{{[0-9_]+}} _{{[0-9_]+}}

// CHECK-DAG:     .con pow_then_{{[0-9_]+}} []{{(@.*)?}}= {
// CHECK-DAG:         _{{[0-9_]+}} 1:(.Idx 4294967296)

// CHECK-DAG:     .con pow_cont_{{[0-9_]+}} _{{[0-9_]+}}: .Idx 4294967296{{(@.*)?}}= {
// CHECK-DAG:         .let _{{[0-9_]+}}: .Idx 4294967296 = %core.wrap.mul 4294967296 0 (42:(.Idx 4294967296), _{{[0-9_]+}});
// CHECK-DAG:         _{{[0-9_]+}} _{{[0-9_]+}}

// CHECK-DAG:     .con pow_else_{{[0-9_]+}} []{{(@.*)?}}= {
// CHECK-DAG:         .let _{{[0-9_]+}}: .Idx 4294967296 = %core.wrap.add 4294967296 0 (4294967295:(.Idx 4294967296), b_{{[0-9_]+}});
// CHECK-DAG:         pow_{{[0-9_]+}} (_{{[0-9_]+}}, pow_cont_{{[0-9_]+}})

// CHECK-DAG:     .let _{{[0-9_]+}}: .Idx 2 = %core.icmp.xyglE 4294967296 (0:(.Idx 4294967296), b_{{[0-9_]+}});
// CHECK-DAG:     (pow_else_{{[0-9_]+}}, pow_then_{{[0-9_]+}})#_{{[0-9_]+}} ()
