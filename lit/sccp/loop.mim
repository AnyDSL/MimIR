// RUN: %mim %s --output-ll - | FileCheck %s
plugin core;

cfun dont_know[]: Bool;

fun extern f(x: Nat): Nat =
    head (23, x, 3) where
        con head(c x t: Nat) =
            ret cond = dont_know $ ();
            (exit, body)#cond () where
                con body() =
                    let cond       = %core.ncmp.e (c, 23);
                    (false, true)#cond ()
                    where
                        con true () =
                            next (c, x, 4);

                        con false() =
                            let c = %core.nat.add (c, 1);
                            let x = %core.nat.add (x, 1);
                            next (c, x, t);
                        con next (c x t: Nat) as cxt = head cxt;
                    end;

                con exit() =
                    let r = %core.nat.add (c, x);
                    let s = %core.nat.add (r, t);
                    return s;
            end
    end;

lam extern _compile(): %compile.Phase =
    %compile.phases ff (
        %compile.internal_cleanup_phase,
        %compile.phases tt (
            %compile.beta_red_phase,
            %compile.eta_red_phase,
        ),
        %compile.eta_exp_phase,
        %compile.sccp,
    );

// CHECK: define i64 @f(i64 [[X:%x_[0-9]+]])
// CHECK: [[T:%t_[0-9]+]] = phi i64 [ 3, %f_{{[0-9]+}} ], [ 4, %body_{{[0-9]+}} ]
// CHECK: [[R:%r_[0-9]+]] = add nsw nuw i64 23, [[X]]
// CHECK: [[S:%s_[0-9]+]] = add nsw nuw i64 [[T]], [[R]]
// CHECK: ret i64 [[S]]
