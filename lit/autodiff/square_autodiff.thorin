// RUN: rm -f %t.ll ; \
// RUN: %thorin -d direct -d autodiff %s --output-ll %t.ll -o - | FileCheck %s

.import core;
.import autodiff;
.import mem;

.let i32 = .Idx 4294967296;

.cn f [mem: %mem.M, a:i32, ret: .Cn [%mem.M, i32]] = {
    .let b = %core.wrap.mul (0:.Nat, 4294967296:.Nat) (a, a);
    ret (mem, b)
};

.cn .extern main [mem : %mem.M, argc : i32, argv : %mem.Ptr (%mem.Ptr (.Idx 256, 0:.Nat), 0:.Nat), return : .Cn [%mem.M, i32]] = {

    .cn ret_cont [mem: %mem.M, r:i32, pb:.Cn[%mem.M, i32,.Cn[%mem.M, i32]]] = {
        .cn pb_ret_cont [mem: %mem.M, pr:i32] = {
            .let c = %core.wrap.mul (0:.Nat, 4294967296:.Nat) (100:i32, r);
            .let d = %core.wrap.add (0:.Nat, 4294967296:.Nat) (c, pr);
            return (mem, d)
        };

        pb(mem, 1:i32 ,pb_ret_cont)
    };

    .let f_diff = %autodiff.autodiff (.Cn [%mem.M, i32,.Cn[%mem.M, i32]]) f;
    .let f_diff_cast =  f_diff;

    f_diff_cast (mem, 42:i32, ret_cont)
};

// CHECK-DAG: return{{.*}}176484
