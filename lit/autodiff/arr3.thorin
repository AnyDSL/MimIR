// RUN: rm -f %t.ll ; \
// RUN: %thorin -d autodiff %s --output-ll %t.ll --output-thorin - | FileCheck %s

// a call to a autodiff style function
// ./build/bin/thorin -d debug -d direct -d autodiff ./lit/autodiff/multiply_autodiff.thorin --output-thorin - -VVVV

.import core;
.import autodiff;
.import mem;
.import affine;

.let i32 = .Idx 4294967296;
.let size = 100:.Nat;
.let size_idx = (.Idx 100);
.let size_int = 100:i32;
.let arr_size = <<size; i32>>;
.let ptr_ty = %mem.Ptr (arr_size, 0);

.cn printInteger [mem: %mem.M, val: i32, return : .Cn [%mem.M]];

.cn f [mem : %mem.M, a: ptr_ty, b: ptr_ty, c: ptr_ty, ret: .Cn [mem : %mem.M]] = {
    
    .cn for_exit [mem : %mem.M] = {
        ret (mem)
    };

    .cn for_body [i : i32, mem : %mem.M, continue : .Cn [%mem.M]] = {
        .let idx_100_i = %core.bitcast (size_idx, i32) i;

        .let lea_a = %mem.lea (size, <size; i32>, 0) (a, idx_100_i);
        .let lea_b = %mem.lea (size, <size; i32>, 0) (b, idx_100_i);
        .let lea_c = %mem.lea (size, <size; i32>, 0) (c, idx_100_i);

        .let (load_mem1, load_val1) = %mem.load (i32, 0) (mem, lea_a);
        .let (load_mem2, load_val2) = %mem.load (i32, 0) (load_mem1, lea_b);
        .let prod = %core.wrap.mul (0:.Nat, 4294967296:.Nat) (load_val1, load_val2);

        .let store_mem = %mem.store (i32, 0) (load_mem2, lea_c, prod);
        continue (store_mem)
    };
    
    %affine.For (4294967296, 1, (%mem.M)) (0:i32, size_int, 1:i32, (mem), for_body, for_exit)
};

.cn init [mem: %mem.M, ptr : ptr_ty, offset : i32, ret : .Cn [%mem.M]] = {
    .cn for_exit [mem : %mem.M] = {
        ret (mem)
    };

    .cn for_body [i : i32, mem : %mem.M, continue : .Cn [%mem.M]] = {
        .let idx_100_i = %core.bitcast (size_idx, i32) i;
        .let lea = %mem.lea (size, <size; i32>, 0) (ptr, idx_100_i);

        .let sum = %core.wrap.add (0:.Nat, 4294967296:.Nat) (offset, i);
        .let store_mem = %mem.store (i32, 0) (mem, lea, sum);
        continue (store_mem)
    };

    %affine.For (4294967296, 1, (%mem.M)) (0:i32, size_int, 1:i32, (mem), for_body, for_exit)
};

.cn const [mem: %mem.M, ptr : ptr_ty, value : i32, ret : .Cn [%mem.M]] = {
    .cn for_exit [mem : %mem.M] = {
        ret (mem)
    };

    .cn for_body [i : i32, mem : %mem.M, continue : .Cn [%mem.M]] = {
        .let idx_100_i = %core.bitcast (size_idx, i32) i;
        .let lea = %mem.lea (size, <size; i32>, 0) (ptr, idx_100_i);
        .let store_mem = %mem.store (i32, 0) (mem, lea, value);
        continue (store_mem)
    };

    %affine.For (4294967296, 1, (%mem.M)) (0:i32, size_int, 1:i32, (mem), for_body, for_exit)
};

.cn .extern main [mem : %mem.M, argc : i32, argv : %mem.Ptr (%mem.Ptr (.Idx 256, 0:.Nat), 0:.Nat), return : .Cn [%mem.M, i32]] = {

    .let (alloc_a_mem, a) = %mem.alloc (arr_size, 0) mem;
    .let (alloc_b_mem, b) = %mem.alloc (arr_size, 0) alloc_a_mem;
    .let (alloc_c_mem, c) = %mem.alloc (arr_size, 0) alloc_b_mem;
    .let (alloc_ad_mem, ad) = %mem.alloc (arr_size, 0) alloc_c_mem;
    .let (alloc_bd_mem, bd) = %mem.alloc (arr_size, 0) alloc_ad_mem;
    .let (alloc_cd_mem, cd) = %mem.alloc (arr_size, 0) alloc_bd_mem;

    .cn ret_cont [mem : %mem.M, pb:.Cn[%mem.M, ptr_ty, ptr_ty, ptr_ty,.Cn[%mem.M, ptr_ty, ptr_ty, ptr_ty]]] = {
        .cn pb_ret_cont [mem : %mem.M, ad: ptr_ty, bd: ptr_ty, cd: ptr_ty] = {
            .cn print_integer_callback2 [mem : %mem.M] = {
                .let free_mem = %mem.free (arr_size, 0) (mem, a);
                return (free_mem, 0:i32)
            };

            .cn print_integer_callback [mem : %mem.M] = {
                .let lea_bd = %mem.lea (size, <size; i32>, 0) (bd, 1:size_idx);
                .let (load_mem2, load_val) = %mem.load (i32, 0) (mem, lea_bd);
                printInteger ( load_mem2, load_val, print_integer_callback2 )
            };

            .cn print_integer_backward [mem : %mem.M] = {
                .let lea_ad = %mem.lea (size, <size; i32>, 0) (ad, 1:size_idx);
                .let (load_mem1, load_val) = %mem.load (i32, 0) (mem, lea_ad);
                printInteger ( load_mem1, load_val, print_integer_callback )
            };

            printInteger ( mem, 22:i32, print_integer_backward )
        };

        .cn print_integer_forward [mem : %mem.M] = {
            pb(mem, ad, bd, cd, pb_ret_cont)
        };

        .let lea_c = %mem.lea (size, <size; i32>, 0) (c, 1:size_idx);
        .let (load_mem1, load_val) = %mem.load (i32, 0) (mem, lea_c);
        printInteger ( load_mem1, load_val, print_integer_forward )
    };

    .let f_diff = %autodiff.ad (.Cn [%mem.M, ptr_ty, ptr_ty, ptr_ty,.Cn[%mem.M]]) f;

    .cn enter_diff [mem : %mem.M] = {
        f_diff (mem, a, b, c,ret_cont)
    };

    .cn init_cd [mem : %mem.M] = {
        const(mem, cd, 1:i32, enter_diff)
    };

    .cn init_bd [mem : %mem.M] = {
        const(mem, bd, 0:i32, init_cd)
    };

    .cn init_ad [mem : %mem.M] = {
        const(mem, ad, 0:i32, init_bd)
    };

    .cn init_c [mem : %mem.M] = {
        const(mem, c, 0:i32, init_ad)
    };

    .cn init_b [mem : %mem.M] = {
        init(mem, b, 2:i32, init_c)
    };

    .cn init_a [mem : %mem.M] = {
        init(mem, a, 1:i32, init_b)
    };

    printInteger ( alloc_cd_mem, 99:i32, init_a )
};



// CHECK-DAG: .cn .extern main _{{[0-9_]+}}::[mem_[[memId:[_0-9]*]]: %mem.M, (i32), %mem.Ptr (%mem.Ptr ((.Idx 256), 0:.Nat), 0:.Nat), return_[[returnId:[_0-9]*]]: .Cn [%mem.M, (i32)]] = {
// CHECK-DAG: _[[appId:[_0-9]*]]: ⊥:★ = return_[[returnEtaId:[_0-9]*]] (mem_[[memId]], 42:(i32));
// CHECK-DAG: _[[appId]]

// CHECK-DAG: return_[[returnEtaId]] _[[returnEtaVarId:[0-9_]+]]: [%mem.M, (i32)] = {
// CHECK-DAG: return_[[retAppId:[_0-9]*]]: ⊥:★ = return_[[returnId]] _[[returnEtaVarId]];
// CHECK-DAG: return_[[retAppId]]
