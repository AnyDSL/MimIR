// RUN: rm -f %t.ll ; \
// RUN: %thorin %s --output-ll %t.ll -o - | FileCheck %s
// RUN: clang %t.ll -o %t -Wno-override-module
// RUN: %t ; test $? -eq 0
// RUN: %t 1 2 3 ; test $? -eq 6

.import core;
.import mem;

.con .extern main(mem: %mem.M, argc: .Idx 4294967296, argv: %mem.Ptr (%mem.Ptr (.Idx 256, 0), 0), return: .Cn [%mem.M, .Idx 4294967296]) = {
    .con loop(mem: %mem.M, i: .Idx 4294967296, acc: .Idx 4294967296) = {
        .let cond: (.Idx 2) = %core.icmp.ul (i, argc);
        .con body m: %mem.M = {
            .let inc  = %core.wrap.add 0 (1:(.Idx 4294967296), i);
            .let acci = %core.wrap.add 0 (i, acc);
            loop (m, inc, acci)
        };
        (.cn m: %mem.M = return (m, acc), body)#cond mem
    };
    loop (mem, 0:(.Idx 4294967296), 0:(.Idx 4294967296))
};

// CHECK-DAG: .con .extern main _[[mainVarId:[0-9_]+]]::[mem_[[memId:[0-9_]+]]: %mem.M, argc_[[argcId:[0-9_]+]]: .Idx 4294967296, %mem.Ptr (%mem.Ptr (.Idx 256, 0), 0), return_[[returnId:[0-9_]+]]: .Cn [%mem.M, .Idx 4294967296]] = {
// CHECK-DAG: loop_[[loopId:[0-9_]+]] (mem_[[memId]], 0:(.Idx 4294967296), 0:(.Idx 4294967296))

// CHECK-DAG: .con return_[[returnEtaId:[0-9_]+]] _[[returnEtaVarId:[0-9_]+]]: [%mem.M, .Idx 4294967296] = {
// CHECK-DAG: return_[[returnId]] _[[returnEtaVarId]]

// CHECK-DAG: .con loop_[[loopId]] _{{[0-9_]+}}::[mem_[[loopMemId:[0-9_]+]]: %mem.M, i_[[iterId:[0-9_]+]]: .Idx 4294967296, acc_[[accId:[0-9_]+]]: .Idx 4294967296] = {
// CHECK-DAG:   _[[condId:[0-9_]+]]: .Idx 2 = %core.icmp.XygLe 4294967296 (i_[[iterId]], argc_[[argcId]]);
// CHECK-DAG: (_[[exitId:[0-9_]+]], body_[[bodyId:[0-9_]+]])#_[[condId]] mem_[[loopMemId]]

// CHECK-DAG: .con _[[exitId]] m_[[mExitVarId:[0-9_]+]]: %mem.M = {
// CHECK-DAG: return_[[returnEtaId]] (m_[[mExitVarId]], acc_[[accId]])

// CHECK-DAG: .con body_[[bodyId]] m_[[mBodyVarId:[0-9_]+]]: %mem.M = {
// CHECK-DAG:   _[[addIterId:[0-9_]+]]: .Idx 4294967296 = %core.wrap.add 4294967296 0 (1:(.Idx 4294967296), i_[[iterId]]);
// CHECK-DAG:   _[[addAccId:[0-9_]+]]: .Idx 4294967296 = %core.wrap.add 4294967296 0 (acc_[[accId]], i_[[iterId]]);
// CHECK-DAG: loop_[[loopId]] (m_[[mBodyVarId]], _[[addIterId]], _[[addAccId]])
