// RUN: rm -f %t.ll
// RUN: %mim %s -O 0

plugin core;
plugin gpu;

fun kernel (m1: %gpu.GlobalM, m3: %gpu.SharedM, m4: %gpu.ConstM, m5: %gpu.LocalM,
            group_id: Idx 4, item_id: Idx 4, data: %gpu.GlobalPtr I32)
         : [%gpu.GlobalM, %gpu.SharedM, %gpu.ConstM, %gpu.LocalM] =
    let (m3, shared_ptr) = %mem.slot («4; I32», 3) (m3, 0);
    let store_loc = %mem.lea (shared_ptr, item_id);
    let val = %core.bitcast I32 item_id;
    let m3 = %mem.store (m3, store_loc, val);
    let (m1, m3) = %gpu.sync_work_items (m1, m3);
    let load_idx = %core.wrap.sub %core.mode.nusw (3_4, item_id);
    let load_loc = %mem.lea (shared_ptr, load_idx);
    let (m3, load_val) = %mem.load (m3, load_loc);
    let m1 = %mem.store (m1, data, load_val);
    return (m1, m3, m4, m5);

fun extern main (m0: %mem.M 0, m1: %gpu.GlobalM, argc: I32, argv: %mem.Ptr («⊤:Nat; %mem.Ptr («⊤:Nat; I8», 0)», 0))
              : [%mem.M 0, %gpu.GlobalM, I32] =
    let (m0, ptr) = %mem.alloc (I32, 0) m0;
    let m0 = %mem.store (m0, ptr, argc);

    let (m1, d_ptr) = %mem.alloc (I32, 1) m1;
    let (m0, m1) = %gpu.copy_to_device (m0, m1, ptr, d_ptr);

    ret m0 = %gpu.launch (m0, 4, 4, %gpu.DefaultStream) kernel $ d_ptr;

    let (m0, m1) = %gpu.copy_to_host (m0, m1, d_ptr, ptr);
    let m1 = %mem.free (m1, d_ptr);

    let (m0, val) = %mem.load (m0, ptr);
    let m0 = %mem.free (m0, ptr);

    return (m0, m1, val);
